plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id "io.github.ladysnake.chenille" version "0.14.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

chenille {
	configurePublishing {
		withLadysnakeMaven()
		withGithubRelease()
		withModrinthRelease()
	}
}

repositories {
	mavenLocal()

	// Modrinth
	maven { url "https://api.modrinth.com/maven" }

	// Mod Menu
	maven { url "https://maven.terraformersmc.com/releases/" }

	// CCA, Ratatouille
	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases'
	}

	// Datasync
	maven { url = "https://maven.uuid.gg/releases" }
}

loom {
	accessWidenerPath = file("src/main/resources/trainmurdermystery.accesswidener")
}

fabricApi {
	configureDataGeneration() {
		modId = "trainmurdermystery"
		client = true
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Libraries
	// Ratatouille
	modIncludeImplementation "dev.upcraft.datasync:datasync-minecraft-${project.minecraft_version}-fabric:${project.datasync_version}"
	modImplementation "dev.doctor4t:ratatouille:${project.ratatouille_version}"
	// CCA
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-base:${project.cca_version}"
	// Adds a dependency on a specific module
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-world:${project.cca_version}"
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-entity:${project.cca_version}"
	// Includes Cardinal Components API as a Jar-in-Jar dependency
	include "org.ladysnake.cardinal-components-api:cardinal-components-base:${project.cca_version}"
	include "org.ladysnake.cardinal-components-api:cardinal-components-world:${project.cca_version}"
	include "org.ladysnake.cardinal-components-api:cardinal-components-entity:${project.cca_version}"


	// Runtime mods
	modLocalImplementation "com.terraformersmc:modmenu:${mod_menu_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}